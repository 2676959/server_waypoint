plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.10-SNAPSHOT' apply false
	id 'com.replaymod.preprocess' version '0a04a5668b'
}

preprocess {
	strictExtraMappings = false

	def mc121 = createNode('1.21', 1_21, '')
	def mc1213 = createNode('1.21.3', 1_21_03, '')
	def mc1215 = createNode('1.21.5', 1_21_05, '')
	def mc1218 = createNode('1.21.8', 1_21_08, '')

	mc121.link(mc1213, file('versions/mapping-1.21-1.21.3.txt'))
	mc1213.link(mc1215, file('versions/mapping-1.21.3-1.21.5.txt'))
	mc1215.link(mc1218, file('versions/mapping-1.21.5-1.21.8.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}
}